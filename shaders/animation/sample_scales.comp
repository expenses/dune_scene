
#version 450

#include "../includes/structs.glsl"

layout(set = 0, binding = 1) writeonly buffer LocalTransforms {
    Similarity local_transforms[];
};

layout(set = 0, binding = 3) readonly buffer AnimationStates {
    AnimationState animation_states[];
};

layout(set = 1, binding = 0) readonly buffer ScaleInputs {
    float scale_inputs[];
};

layout(set = 1, binding = 1) readonly buffer ScaleOutputs {
    float scale_outputs[];
};

layout(set = 1, binding = 2) readonly buffer ScaleChannels {
    Channel scale_channels[];
};

#define CHANNEL_INPUTS scale_inputs
#define CHANNEL_OUTPUTS scale_outputs
#define SAMPLE_TYPE float
#include "sampling.glsl"

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

void main() {
    uint index = gl_GlobalInvocationID.x;

    if (index >= scale_channels.length()) {
        return;
    }

    uint channels_per_animation = scale_channels.length() / animation_states.length();

    uint animation_index = index / channels_per_animation;

    uint nodes_per_animation = local_transforms.length() / animation_states.length();

    uint node_index = animation_index * nodes_per_animation;

    float time = animation_states[animation_index].time;

    Channel channel = scale_channels[index];
    float scale;
    bool invalid;
    sample_cubic_spline_float(time, channel, scale, invalid);
    if (!invalid) {
        local_transforms[node_index + channel.node_index].scale = scale;
    }
}
