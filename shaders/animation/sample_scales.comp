
#version 450

#include "../includes/structs.glsl"

layout(set = 0, binding = 1) uniform AnimationInfo {
    uint num_joints;
    uint num_nodes;
};

layout(set = 0, binding = 2) writeonly buffer LocalTransforms {
    Similarity local_transforms[];
};

layout(set = 0, binding = 3) readonly buffer AnimationStates {
    AnimationState animation_states[];
};

layout(set = 1, binding = 0) readonly buffer ScaleInputs {
    float scale_inputs[];
};

layout(set = 1, binding = 1) readonly buffer ScaleOutputs {
    float scale_outputs[];
};

layout(set = 1, binding = 2) readonly buffer ScaleChannels {
    Channel scale_channels[];
};

#define CHANNEL_INPUTS scale_inputs
#define CHANNEL_OUTPUTS scale_outputs
#define SAMPLE_TYPE float
#include "sampling.glsl"

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

void main() {
    uint index = gl_GlobalInvocationID.x;

    uint channels_per_animation = scale_channels.length();

    uint animation_index = index / channels_per_animation;
    uint channel_index = index % channels_per_animation;
    uint node_index = animation_index * num_nodes;

    if (animation_index >= animation_states.length()) {
        return;
    }

    float time = animation_states[animation_index].time;

    Channel channel = scale_channels[channel_index];
    float scale;
    bool invalid;
    sample_cubic_spline(time, channel, scale, invalid);
    if (!invalid) {
        local_transforms[node_index + channel.node_index].scale = scale;
    }
}
