#version 450

#include "structs.glsl"

layout(set = 0, binding = 0) buffer Ships {
    Ship ships[];
};

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

mat3 rotation_matrix_y(float theta) {
    return mat3(
        cos(theta), 0, sin(theta),
        0, 1, 0,
        -sin(theta), 0, cos(theta)
    );
}

const float LIMIT = 2.0;

void main() {
    uint index = gl_GlobalInvocationID.x;

    if (index >= ships.length()) {
        return;
    }

    Ship ship = ships[index];

    ship.facing += ship.rotation_speed;
    ship.position += vec3(cos(ship.facing), 0.0, sin(ship.facing)) * 0.005;

    vec2 ship_horizontal = ship.position.xz;
    bvec2 is_over_limit = greaterThan(abs(ship_horizontal), vec2(LIMIT));
    vec2 offset = vec2(is_over_limit) * sign(ship_horizontal) * (LIMIT * 2.0);
    ship.position.xz = ship_horizontal - offset;

    ship.y_rotation_matrix = rotation_matrix_y(ship.facing);

    ships[index] = ship;
}

